" Title: .vimrc
" Author: Vincevrp | github.com/Vincevrp

" Plugins {{{
call plug#begin('~/.vim/plugged')
" Essential
Plug 'arcticicestudio/nord-vim'
Plug 'tpope/vim-fugitive'
Plug 'tpope/vim-surround'
Plug 'tpope/vim-repeat'
Plug 'tpope/vim-commentary'
Plug 'junegunn/fzf.vim'

" Completion
Plug 'w0rp/ale'
Plug 'neoclide/coc.nvim',       { 'branch': 'release' }
Plug 'neoclide/coc-json',       { 'for': 'json'       }
Plug 'neoclide/coc-yaml',       { 'for': 'yaml'       }

" Tools
Plug 'junegunn/goyo.vim',       { 'on': 'Goyo' }
Plug 'junegunn/vim-easy-align'
Plug 'justinmk/vim-sneak'
Plug 'takac/vim-hardtime'

" Languange and filetype specific
Plug 'sheerun/vim-polyglot'
Plug 'plasticboy/vim-markdown', { 'for': 'markdown' }
Plug 'rodjek/vim-puppet',       { 'for': 'puppet'   }
Plug 'lervag/vimtex',           { 'for': 'tex'      }
call plug#end()
" }}}

" Vim behaviour {{{
set expandtab                       " Expand tab character to spaces
set softtabstop=4                   " Set columns to 0 for tabs
set shiftwidth=4                    " Make indent correspond to 4 spaces
set smarttab                        " Enable smarttab
set number relativenumber           " Enable relative line numbers
set ignorecase                      " Ignore case when searching
set smartcase                       " Ignore if lowercase, otherwise don't
set hlsearch                        " Highlight search terms
set incsearch                       " Show search matches as you type
set scrolloff=5                     " Lines to keep above and below cursor
set showmatch                       " Show matching parenthesis
set mouse=                          " Mouse input (empty=disabled)
set timeoutlen=1000                 " Set mapping delay (Escape delay fix)
set ttimeoutlen=0                   " Set key code delay (Escape delay fix)
set encoding=utf-8                  " Enable UTF-8
set t_Co=256                        " Enable 256 color support
set noshowcmd                       " Don't show command while typing
set conceallevel=2                  " Enable concealing
set foldmethod=marker               " Enable folding
set wrap                            " Enable visual word wrap
set linebreak                       " Only wrap at breakat characters
set breakindent                     " Indent breaks
set showbreak=↪\                    " Display ↪\ in front of wrapped lines
set splitbelow                      " Open horizontal split below
set cursorline                      " Highlight current line
set nrformats-=octal                " Remove octal numbers from increment
set wildmenu                        " Show wildmenu when using tab completion
set laststatus=2                    " Enable statusline
set noshowmode                      " Disable default mode indicator
set noruler                         " Disable default ruler
set shortmess=actTIO                 " Set shortmess
set noautochdir                     " Don't auto change working directory
set undofile                        " Enable persistent undo
set undodir=~/.vim/undodir          " Set undodir
set viminfo+=n~/.vim/viminfo        " Move viminfo file
set ttyfast                         " Enable fast terminal connection
set lazyredraw                      " Only redraw when necessary
set list                            " Show listchars
set listchars=tab:▶▬                " Show tab characters
set hidden                          " Switch buffers without saving
set confirm                         " Ask to save when exiting

" Miscellaneous
syntax on                           " Enable syntax highlighting
scriptencoding utf-8                " Set script encoding to utf-8
" }}}

" Autocmds {{{

" Highlight trailing whitespace and TAB characters
augroup HighlightTrailingWhitespace
    autocmd!
    autocmd BufWinEnter * match Error /\s\+$/
    autocmd InsertEnter * match Error /\s\+\%#\@<!$/
    autocmd InsertLeave * match Error /\s\+$/
    autocmd BufWinLeave * call clearmatches()
augroup END
" }}}

" Functions and commands {{{

" Loop back to beginning for cnext and cprev
command! Cnext try | cnext | catch | cfirst | catch | endtry
command! Cprev try | cprev | catch | clast  | catch | endtry

cabbrev cnext Cnext
cabbrev cn    Cnext
cabbrev cprev Cprev
cabbrev cp    Cprev

" Open help in new tab
cabbrev help tab help

" Write as sudo
command! W execute ":w !sudo tee %"

" Change cursor in insert mode
if exists('$TMUX')
    let &t_SI = "\ePtmux;\e\e[5 q\e\\"
    let &t_EI = "\ePtmux;\e\e[2 q\e\\"
    let &t_SR = "\ePtmux;\e\e[4 q\e\\"
else
    let &t_SI = "\e[5 q"
    let &t_EI = "\e[2 q"
    let &t_SR = "\e[4 q"
endif

function! SetIndent(num) abort
    execute 'setlocal shiftwidth='.a:num
    execute 'setlocal softtabstop='.a:num
endfun

" LF file explorer
function! LF() abort
    let l:file = '/tmp/vim-ranger'
    execute 'silent !lf -selection-path='.l:file
    if filereadable(l:file)
        execute 'edit'.system('<'.l:file.'&& rm '.l:file)
    endif
    redraw!
endfun

" Use appropriate FZF function
function! CheckFZF() abort
    let status = system('git rev-parse --is-inside-work-tree')
    execute status =~# 'true' ? 'GFiles' : 'Files'
endfun

function! ToggleColorColumn() abort
    execute 'set colorcolumn='. (&colorcolumn ==# '' ? '80' : '')
endfun

function! ToggleQuickFix() abort
    for i in range(1, winnr('$'))
        if getbufvar(winbufnr(i), '&buftype') ==# 'quickfix'
            cclose
            return
        endif
    endfor

    copen
endfunction

function! RemoveTrailingWhitespace() abort
    let l =1
    for line in getline(1, '$')
        call setline(l, substitute(line, '\s\+$', '', 'g'))
        let l = l+1
    endfor
endfunction
" }}}

" Keybindings {{{
let mapleader=' '

" Add J and K motions to jumplist
nnoremap <expr> k (v:count > 1 ? "m'" . v:count : '') . 'k'
nnoremap <expr> j (v:count > 1 ? "m'" . v:count : '') . 'j'

" Ctrl-keybindings
nnoremap <silent> <C-e> 2<C-e>
nnoremap <silent> <C-y> 2<C-y>
nnoremap <silent> <C-n> :Cnext<CR>
nnoremap <silent> <C-b> :Cprev<CR>
nnoremap <silent> <C-m> :nohlsearch<CR>
nnoremap <silent> <C-p> :call CheckFZF()<cr>
nnoremap <silent> <C-s> :call RemoveTrailingWhitespace()<CR>

" Leader-keybindings
nnoremap <silent> <leader><leader> <C-^>
nnoremap <silent> <leader>r :source ~/.vim/vimrc<CR>
nnoremap <silent> <leader>h :new<CR>
nnoremap <silent> <leader>v :vnew<CR>
nnoremap <silent> <leader>g :Goyo<CR>
nnoremap <silent> <leader>b :BLines<CR>
nnoremap <silent> <leader>l :Lines<CR>
nnoremap <silent> <leader>q :Buffers<CR>
nnoremap <silent> <leader>f :call LF()<CR>
nnoremap <silent> <leader>e :call ToggleQuickFix()<CR>
nnoremap <silent> <leader>w :call ToggleColorColumn()<CR>
nnoremap <silent> <leader>s :call system('xclip -selection clipboard', @0)<CR>

" F-keys
nnoremap <silent> <F1> :set nospell<CR>
nnoremap <silent> <F2> :set spell spelllang=en_us<CR>
nnoremap <silent> <F3> :set spell spelllang=nl<CR>
nnoremap <silent> <F4> :set ff=unix<CR>

" Insert mode
inoremap <C-j> <C-n>
inoremap <C-k> <C-p>

" Disabled
nnoremap q:  <nop>
nnoremap Q   <nop>
nnoremap qq  <nop>
" }}}
